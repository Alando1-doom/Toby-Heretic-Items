ACTOR ArtiEgg_Toby : CustomInventory replaces ArtiEgg
{
	Game Heretic
	SpawnID 14
	+COUNTITEM
	+FLOATBOB
	+INVENTORY.INVBAR
	+INVENTORY.PICKUPFLASH
	Inventory.Icon "ARTIEGGC"
	Inventory.PickupSound "artifact/ovum"
	Inventory.UseSound "artifact/ovum/use"
	Inventory.PickupMessage "$TXT_ARTIEGG"
	Inventory.DefMaxAmount
	Tag "$TAG_ARTIEGG"
	States
	{
	Spawn:
		EGGC A 3 Bright A_SpawnItemEx("EggPickupChecker", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 0)
		EGGC BCBABCBABCB 3 Bright
		Loop
	Pickup:
		TNT1 A 1 A_JumpIfInventory("ArtiEgg_Toby", 16, "NoPickup")
		TNT1 A 1 A_PlaySoundEx("artifact/ovum/ann", "Auto", 0, -1)
		Stop
	NoPickup:
		TNT1 A 1 A_PlaySoundEx("stat/full", "SoundSlot6", 0, 0)
		Fail
	Use:
		TNT1 A 0 A_FireCustomMissile("EggFX", -15, 0, 0, 0, 1)
		TNT1 A 0 A_FireCustomMissile("EggFX", -7.5, 0, 0, 0, 1)
		TNT1 A 0 A_FireCustomMissile("EggFX", 0, 0, 0, 0, 1)
		TNT1 A 0 A_FireCustomMissile("EggFX", 7.5, 0, 0, 0, 1)
		TNT1 A 0 A_FireCustomMissile("EggFX", 15, 0, 0, 0, 1)
		Stop
	}
}